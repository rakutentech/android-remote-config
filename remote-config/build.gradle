apply from: '../config/android/library.gradle'
apply plugin: "kotlin-android"
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'

android {
  sourceSets.each {
    it.java.srcDirs += "src/$it.name/kotlin"
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  defaultConfig {
    consumerProguardFiles 'proguard-rules.pro'
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$CONFIG.versions.kotlin"
  implementation "com.squareup.okhttp3:okhttp:$CONFIG.versions.okhttp"
  compileOnly 'androidx.annotation:annotation:1.0.1'
  implementation "com.rakuten.tech.mobile:manifest-config-annotations:0.1.0"
  kapt "com.rakuten.tech.mobile:manifest-config-processor:0.1.0"
  implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.13.0"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0"
  implementation 'com.rakuten.tech.mobile.sdkutils:sdk-utils:0.1.1'

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.robolectric:robolectric:4.3'
  testImplementation "org.amshove.kluent:kluent-android:1.48"
  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
  testImplementation "org.mockito:mockito-inline:2.28.2"
  testImplementation "com.squareup.okhttp3:mockwebserver:$CONFIG.versions.okhttp"
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0'
  testImplementation 'androidx.test:core:1.0.0'
}

apply from: '../gradle/documentation.gradle'

apply from: '../gradle/maven-publication.gradle'
publishing {
  publications {
    RemoteConfig(MavenPublication, androidArtifact())
  }
}

def isSnapshot = project.version.contains('-')
if (isSnapshot) {
  apply from: '../config/publish/artifactory.gradle'
} else {
  apply from: '../config/publish/bintray.gradle'
}

apply from: "../config/quality/jacoco/android.gradle"
jacoco {
  toolVersion = "0.8.3"
}

apply from: "../config/quality/checkstyle/android.gradle"
apply from: "../config/quality/detekt/android.gradle"
dependencies {
  // Enable the KtLint rules
  detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.0.0-RC14"
}
